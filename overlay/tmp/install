#!/usr/bin/nu

use bf
use bf-php

# Absolute path to FreeScout source directory
const src = "/etc/bf/src/freescout"

def main [] {
    # installed required and recommended extensions
    cd /tmp
    bf write $"Installing PHP extensions."
    bf-php ext install_from_file

    # install FreeScout source files, content directory and default plugins
    bf write debug $"FreeScout source directory: ($src)."
    install_source

    # add bf-freescout module to config
    bf config use bf-freescout

    # cleanup
    bf write "Removing /www files and directories."
    rm -rf /www/*
}

# Download and install FreeScout source files
def install_source [] {
    # add dependencies
    bf write "Adding installation dependencies." install_source
    bf pkg install ["--virtual .install" "git"]

    # get version
    let fs_version = bf fs read FREESCOUT_REVISION

    # save FreeScout version to build log
    bf build add FreeScout $fs_version

    # clone source files
    bf write $"Cloning FreeScout ($fs_version) into ($src)."
    ^git clone --depth 1 --branch $fs_version https://github.com/freescout-helpdesk/freescout $src

    # set permissions
    bf write $"Setting permissions on ($src)." install_source
    [$src "www:www" 0400 0500] | bf ch apply

    # remove installation dependencies
    bf pkg remove [".install"]
}
